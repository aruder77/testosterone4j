grammar de.msg.xt.mdt.tdsl.TDsl with org.eclipse.xtext.xbase.Xbase

generate tDsl "http://www.msg.de/xt/mdt/tdsl/TDsl"
import 'http://www.eclipse.org/xtext/xbase/Xbase' as xbase


TestModel:
	(packages+=PackageDeclaration)*
;

PackageDeclaration:
    'package' name=QualifiedName ('for' sutRef=[SUT|FQN])? '{'
    	(imports+=Import)*
    	(elements+=Element)*
    '}';
    
Import: 
	'import' importedNamespace=FqnWithWildCard
;

FqnWithWildCard: 
	FQN('.*')?
;
    
Element:
	Activity | UseCase | Control | Type | DataType | Test | TagsDeclaration | SUT
;

SUT:
	'sut' name=ID 'using' 'controls' '{'
		controls+=[Control|FQN] (',' controls+=[Control|FQN])*
	'}'
;

Type:
	'type' name=ID 'mappedBy' mappedBy=JvmTypeReference
;

DataType:
	'datatype' name=ID 'type' type=[Type|FQN] '{'
		(classes+=EquivalenceClass)+
	'}'
;

EquivalenceClass:
	'class' name=ID 'value' value=STRING (tags+=[Tag|FQN])* 
;

TagsDeclaration:
	'tags' '{'
		tags+=Tag (',' tags+=Tag)*
	'}'
;

Tag:
	name=ID
;

Control:
	'control' name=ID '{'
		(operations+=Operation)+
	'}'
;

Operation:
	'op' (returnType=[Type|FQN])? name=ID ('(' params+=ControlOperationParameter (',' params+=ControlOperationParameter)* ')')? 
;

ControlOperationParameter:
	type=[Type|FQN] name=ID
;
    
Activity:
	'activity' name=ID ('identifiedBy' uniqueId=STRING)? ('extends' parent=[Activity|FQN])? '{'
		(fields+=Field)*
		(operations+=ActivityOperation)*
	'}'
;

ActivityOperationParameter:
	dataType=[DataType|FQN] name=ID
;

ActivityOperation:
	'op' (returnType=[DataType|FQN])? name=ID ('(' params+=ActivityOperationParameter (',' params+=ActivityOperationParameter)* ')')? (nextActivities+=ConditionalNextActivity)*
;

Field:
	'field' name=ID ('identifiedBy' uniqueId=STRING)? 'control' control=[Control|FQN] ('datatype' dataType=[DataType|FQN])? (tags+=TagWithCondition)* 
	(
		 '{' 
		 (operations+=OperationMapping)+	
		 '}'
	)?
;

TagWithCondition:
	'tag' tag=[Tag|FQN] ('if' condition=XExpression)? 
;

OperationMapping:
	'op' (dataType=[DataType|FQN])? name=[Operation|ID] ('(' (dataTypeMappings+=DataTypeMapping) (',' dataTypeMappings+=DataTypeMapping)* ')')? ('guard' guard=XExpression)?  (nextActivities+=ConditionalNextActivity)*
;

DataTypeMapping:
	datatype=[DataType|FQN] name=[ControlOperationParameter|ID]  
;

ConditionalNextActivity:
	'=>' next=[Activity|FQN] ('if' guard=XExpression)?
;

Parameter:
	dataType=[DataType|FQN] name=ID	
;

ParameterAssignment:
	name=[Parameter|FQN] '=' value=XExpression
;

ParameterInput returns xbase::XExpression:
	GenerationSelektor | XExpression
;

/*
 * Parameter for a parameter generation. The generator only takes an array of tags.
 * The takes may be specified directly using 'select(tags TAG_A, TAG_B)' or dynamically
 * via an expression 'select(someXBaseExpression)'.
 */
GenerationSelektor returns xbase::XExpression:
	{GenerationSelektor}
	'select' ('(' (('tags' tags+=[Tag|ID] (',' tags+=[Tag|ID])*) | expression=XExpression) ')' )?
;

OperationParameterAssignment:
	name=[DataTypeMapping|FQN] '=' value=ParameterInput
;

ActivityOperationParameterAssignment:
	name=[ActivityOperationParameter|FQN] '=' value=ParameterInput
;

Test:
	'test' name=ID 'generator' generator=JvmTypeReference 'useCase' useCase=[UseCase|FQN]
;

UseCase:
	'useCase' name=ID ('(' inputParameter+=Parameter (',' inputParameter+=Parameter)* ')')? 'initial' initialActivity=[Activity|FQN]
		block=UseCaseBlock
;

UseCaseBlock returns xbase::XBlockExpression:
	{xbase::XBlockExpression}
  	'{' (expressions+=StatementLine)* '}'
;

StatementLine returns xbase::XExpression:
	{StatementLine}
	statement=Statement
;

Statement returns xbase::XExpression:
	SubUseCaseCall | OperationCall | ActivityOperationCall | VariableDeclaration | IfExpression | '[' XAssignment ']' | Assert //| CompoundStatement
;

VariableDeclaration returns xbase::XVariableDeclaration:
	{xbase::XVariableDeclaration}
	(writeable?='var') =>(=>(type=JvmTypeReference =>name=ValidID) | name=ValidID) ('=' right=Expression)?
;

Expression returns xbase::XExpression:
	'op' OperationCall | GeneratedValueExpression | XExpression 
;

GeneratedValueExpression returns xbase::XExpression:
	{GeneratedValueExpression}
	'generatedValue' '(' param=[DataTypeMapping|FQN] ')'
;


IfExpression returns xbase::XExpression:
	{xbase::XIfExpression}
	'if' '(' if=Expression ')'
	then=UseCaseBlock
	('else' else=UseCaseBlock)?
;


FQN:
	ID ('.' ID)*
;

MFQN:
	ID '.' ID
;

OperationCall returns xbase::XExpression:
	{OperationCall}
	operation=[OperationMapping|MFQN] ('(' (paramAssignment+=OperationParameterAssignment) (',' paramAssignment+=OperationParameterAssignment)* ')')?
;

ActivityOperationCall returns xbase::XExpression:
	{ActivityOperationCall}
	operation=[ActivityOperation|ID] ('(' (paramAssignment+=ActivityOperationParameterAssignment) (',' paramAssignment+=ActivityOperationParameterAssignment)* ')')?
;

SubUseCaseCall returns xbase::XExpression:
	{SubUseCaseCall}
	'call' useCase=[UseCase|FQN] ('(' paramAssignment+=ParameterAssignment (',' paramAssignment+=ParameterAssignment)*  ')')?
;

Assert returns xbase::XExpression:
	{Assert}
	'assert' '[' expression=Expression ']'
;

/*
CompoundStatement:
	'(' statements+=Statement ('|' statements+=Statement)+ ')'
; */


